buildscript {
    ext.groupId = "io.github.syaku"
    ext.versionName = "1.0.0.BUILD-SNAPSHOT"
    // https://docs.spring.io/spring-boot/docs/2.4.5/reference/htmlsingle/
    ext.springBootVersion = "2.4.5"
    ext.junitVersion = "5.7.1"
    ext.restDocsVersion = "2.0.5.RELEASE"
    ext.jacksonVersion = "2.11.4"
    ext.kotlinVersion = "1.4.32"

    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.7.1"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.8.RELEASE"
        classpath "org.asciidoctor:asciidoctor-gradle-plugin:1.5.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}"
        classpath "org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
    }
}

wrapper {
    gradleVersion = "6.5"
}

apply plugin: "kotlin"
apply plugin: "org.jetbrains.kotlin.plugin.spring"
apply plugin: "org.jetbrains.kotlin.plugin.noarg"
apply plugin: "org.jetbrains.kotlin.plugin.jpa"
apply plugin: "org.jetbrains.kotlin.kapt"
apply plugin: "idea"
apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "org.asciidoctor.convert"
apply plugin: "org.sonarqube"
apply plugin: "jacoco"
//apply plugin: "checkstyle"

// IntelliJ IDEA : project build path
idea {
    module {
        inheritOutputDirs = true
        outputDir = compileJava.destinationDir
        testOutputDir = compileTestJava.destinationDir
    }
}

repositories {
    mavenCentral()
    jcenter()
}

group groupId
version versionName

sourceCompatibility = 11
targetCompatibility = 11

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "11"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "11"
    }
}

compileJava.options.encoding = 'UTF-8'

configurations {
    implementation.exclude module: "spring-boot-starter-tomcat"
    testImplementation.exclude group: "junit", module: "junit"
}

processResources {
    filesMatching('*.yml') {
        expand(project.properties)
    }
}

dependencies {
    implementation "org.modelmapper:modelmapper:2.3.0"
    implementation "org.apache.commons:commons-lang3:3.7"

    asciidoctor "org.springframework.restdocs:spring-restdocs-asciidoctor:${restDocsVersion}"
    testImplementation "org.springframework.restdocs:spring-restdocs-mockmvc:${restDocsVersion}"

    implementation "com.fasterxml.jackson.datatype:jackson-datatype-hibernate5:${jacksonVersion}"

    implementation "org.springframework.boot:spring-boot-starter-data-jpa"

    runtimeOnly "org.springframework.boot:spring-boot-devtools"
    kapt "org.springframework.boot:spring-boot-configuration-processor"
    implementation "org.springframework.boot:spring-boot-starter-validation"

    implementation "org.springframework.boot:spring-boot-starter-undertow"
    implementation "org.springframework.boot:spring-boot-starter-web"
    // or implementation "org.springframework.boot:spring-boot-starter"
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }

    testImplementation "com.h2database:h2:1.4.191"
    compileOnly "com.h2database:h2:1.4.191"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
    implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
    implementation "org.jetbrains.kotlin:kotlin-test:${kotlinVersion}"
    implementation "org.jetbrains.kotlin:kotlin-test-junit5:${kotlinVersion}"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.11.4"
}

// <------- RestDocs & Asciidoctor
def snippetsDir = file("${buildDir}/generated-snippets")

asciidoctor {
    attributes "snippets": snippetsDir
    inputs.dir snippetsDir
    dependsOn test
}
// ------- RestDocs & Asciidoctor ->

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.language", "java"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.sources", "src/main/java"
        property "sonar.exclusions", "**/*Test*.*, **/Q*.java"
        property "sonar.test.inclusions", "**/*Test.java"
        property "sonar.coverage.exclusions", "**/*Test*.*, **/Q*.java"
        property "sonar.java.junit.reportPaths", "${buildDir}/test-results"
        property "sonar.jacoco.reportPaths", "${buildDir}/jacoco/jacoco.exec"
    }
}

//checkstyle {
//    configFile = file("./.platform-settings/google-checkstyle.xml")
//    toolVersion = '8.33'
//}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    systemProperty 'spring.profiles.active', 'test'
}

bootJar {
    enabled = true
    mainClassName = "${groupId}.Application"
    launchScript()
}

jar {
    enabled false
}
